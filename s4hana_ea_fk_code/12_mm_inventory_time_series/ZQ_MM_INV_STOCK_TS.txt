/* Freek Keijzer, myBrand, 16.09.2020
In selection screen a.o. "Start Date", "End Date" and "Period Type".
For "Period Type" following options:
- 'D' = 'Calendar Day'
- 'F' = 'Fiscal Year Period'
- 'M' = 'Calendar Month'
- 'P' = 'Period LFWIM' = custom enhancement
- 'Q' = 'Calendar Quarter'
- 'W' = 'Calendar Week'
- 'Y' = 'Calendar Year'

Initial view consists of following key figures:
- "Stock Change Quantity" (within time period)
- "Stock Level Quantity" (at end of time period)
- "Stock Change Value" (within time period)
- "Stock Level Value" (at end of time period)

Following key figures are initially hidden, but can be added:
- "Quantity Issued" (within time period)
- "Quantity Received" (within time period)
- "Value Issued" (within time period)
- "Value Received" (within time period)
- "No Material Document Items" (contributing to Stock Changes within time period)
- "No Days" (within time period)

Dimension "Time Period" in columns.

Location dimensions, hierarchically from large to small:
- "Plant" (matdoc.werks)
- "Storage Location" (matdoc.lgort)
- "Bin Location" (mard.lgpbe)  = custom enhancement

"Plant" and "Material" are initially shown in rows, key + text.

Query is loosely based op standard SAP query-view C_MaterialStockTimeSeries.
--------------------------------------------------------
Change-log: 
--------------------------------------------------------
--------------------------------------------------------*/
@EndUserText.label: 'MM: Stock Time Series - Data'
@AbapCatalog.sqlViewName: 'ZQMMINVSTOCKTS'

@VDM.viewType: #CONSUMPTION
@Analytics.query: true
@Analytics.settings.maxProcessingEffort: #HIGH
@OData.publish: true
@AbapCatalog.compiler.compareFilter: true
@AbapCatalog.preserveKey: true
@AccessControl.authorizationCheck: #CHECK

define view ZQ_MM_INV_STOCK_TS
  with parameters
    @Consumption.hidden: true
    @Environment.systemField: #SYSTEM_LANGUAGE
    P_Language   : sylangu,
    P_StartDate  : vdm_v_start_date,
    P_EndDate    : vdm_v_end_date,
    P_PeriodType : nsdm_period_type

  as select from ZC_MM_INV_STOCK_TS(P_StartDate:  :P_StartDate,
                                           P_EndDate:    :P_EndDate,
                                           P_PeriodType: :P_PeriodType)
{
//KEY FIGURES, UNITS, CURRENCIES
//--Quantities
    MatlStkChangeQtyInBaseUnit,

    MatlWrhsStkQtyInMatlBaseUnit,

    @AnalyticsDetails.query.hidden: true
    GoodsIssueQty,   

    @AnalyticsDetails.query.hidden: true
    GoodsReceiptQty,   

    MaterialBaseUnit,

//--Amounts using current price
    StockChangeValInCCCrcy,
    
    StockValueInCCCrcy,

    @AnalyticsDetails.query.hidden: true
    GoodsIssueVal,   

    @AnalyticsDetails.query.hidden: true
    GoodsReceiptVal,   

    CompanyCodeCurrency,
    
    @AnalyticsDetails.query.hidden: true
    CountMatDocPos,

    @AnalyticsDetails.query.hidden: true
    NDays,

//CHARACTERISTICS IN COLUMNS
    @AnalyticsDetails.query.axis: #COLUMNS
    YearPeriod,

//CHARACTERISTICS IN ROWS
    @Consumption.filter: {selectionType: #SINGLE, multipleSelections: true, mandatory: false}
    @AnalyticsDetails.query.variableSequence : 10
    @AnalyticsDetails.query.axis: #ROWS
    @ObjectModel.text.element: ['MaterialName']
    Material,
  
    @Consumption.filter: {selectionType: #SINGLE, multipleSelections: true, mandatory: false}
    @AnalyticsDetails.query.variableSequence : 20
    @AnalyticsDetails.query.axis: #ROWS
    @ObjectModel.text.element: ['PlantName']
    Plant,

//FREE CHARACTERISTICS
//--Additional characteristics compared to SAP standard
    StockChangeCategory,


//----Material attributes as dimension
    @Consumption.filter: {selectionType: #SINGLE, multipleSelections: true, mandatory: false}
    @AnalyticsDetails.query.variableSequence : 40
    MaterialGroup,
    
    @Consumption.filter: {selectionType: #SINGLE, multipleSelections: true, mandatory: false}
    @AnalyticsDetails.query.variableSequence : 50
    MaterialType,
    
//----Additional field from table MARD
    StorageBin,                                  //Bin Location (mard.lgpbe)

//----Additional field for start of time period
    StartDate,


//--From SAP standard view
  // Period
    PeriodType,
    EndDate,

    @Consumption.filter: {selectionType: #SINGLE, multipleSelections: true, mandatory: false}
    @AnalyticsDetails.query.variableSequence : 30
    @ObjectModel.text.element: ['StorageLocationName']
    StorageLocation,
  
    Batch,
  
    @ObjectModel.text.element: ['SupplierName']
    Supplier,
  
    SDDocument,
  
    SDDocumentItem,
  
    WBSElementInternalID,
  
    @ObjectModel.text.element: ['CustomerName']
    Customer,
  
    @Consumption.filter: {selectionType: #SINGLE, multipleSelections: true, mandatory: false}
    @AnalyticsDetails.query.variableSequence : 70
    @ObjectModel.text.element: ['InventoryStockTypeName']
    InventoryStockType,
  
    @Consumption.filter: {selectionType: #SINGLE, multipleSelections: true, mandatory: false}
    @AnalyticsDetails.query.variableSequence : 80
    @ObjectModel.text.element: ['InventorySpecialStockTypeName']
    InventorySpecialStockType,

  // Stock Groups
    CompanyCode,
    @Semantics.fiscal.yearVariant: true
    FiscalYearVariant,


  // Names and descriptions
    _Material._Text[1: Language=$parameters.P_Language].MaterialName,
    _CompanyCode.CompanyCodeName,
    _Plant.PlantName,
    _StorageLocation.StorageLocationName,
    _Supplier.SupplierName,
    _Customer.CustomerName,
    _InventoryStockType._Text[1: Language=$parameters.P_Language].InventoryStockTypeName,
    _InventorySpecialStockType._Text[1: Language=$parameters.P_Language].InventorySpecialStockTypeName
}                               
