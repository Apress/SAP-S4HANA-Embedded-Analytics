/* Freek Keijzer, myBrand, 24.10.2019
Text-view for Fiscal Period CHAR-3.
Source table:
T009C: "Period names"

Extra values for period ‘000’.
--------------------------------------------------------
Change-log: 
--------------------------------------------------------
--------------------------------------------------------*/
@EndUserText.label: 'FI: Fiscal Period texts'
@AbapCatalog.sqlViewName: 'ZTFIFISCPER'

@ObjectModel.dataCategory: #TEXT
@AccessControl.authorizationCheck: #NOT_REQUIRED
@VDM.viewType: #BASIC
@ClientHandling.algorithm: #SESSION_VARIABLE
@Metadata.ignorePropagatedAnnotations: true
@ObjectModel.usageType: {
  dataClass: #CUSTOMIZING,
  serviceQuality: #A,
  sizeCategory: #S
}

define view ZT_FI_FISCPER as select from ZB_FI_FISCPER_TXT
association [0..1] to I_Language as _Language on $projection.Language = _Language.Language
{
    @ObjectModel.foreignKey.association: '_Language'
    @Semantics.language
key spras            as Language,
key FiscPer          as FiscPer,
    @Semantics.text
    ktext            as FiscPerName,
    @Semantics.text
    ltext            as FiscPerLongName,
    _Language
}
